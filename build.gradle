plugins {
    id "application"
    id "distribution"
    id "idea"
    id "jacoco"
    id "com.github.johnrengelman.shadow" version "5.1.0"
    id "com.diffplug.gradle.spotless" version "3.24.3"
}

repositories {
    maven { url 'http://dl.bintray.com/web3j/maven' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url  "https://dl.bintray.com/ethereum/maven" }

    jcenter()
}

ext {
    web3jVersion = "4.+"
    slf4jVersion = "1.7.+"
    mockitoVersion = "3.+"
    junitVersion = '5.+'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

dependencies {
    implementation "org.web3j:codegen:$web3jVersion",
            "org.web3j:contracts:$web3jVersion",
            "org.web3j:core:$web3jVersion",
            "org.web3j:crypto:$web3jVersion",
            "org.web3j:hosted-providers:$web3jVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    implementation("org.web3j:web3j-unit:4.6.0-SNAPSHOT") {

        exclude group: 'org.junit.jupiter'
    }
    runtime "org.slf4j:slf4j-nop:$slf4jVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion",
            "org.mockito:mockito-core:$mockitoVersion"
}

spotless {
    java {
        target fileTree('.') {
            include '**/src/*/java/**/*.java'
            exclude '**/.gradle/**'
            exclude '**/generated/**'
        }
        removeUnusedImports()
        googleJavaFormat("1.7").aosp()
        importOrder 'java', '', 'org.web3j', '\\#'
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile "$rootDir/gradle/spotless.java.license"
        paddedCell()
    }
    groovyGradle {
        target '*.gradle'
        greclipse().configFile(rootProject.file('gradle/formatter.properties'))
        endWithNewline()
        indentWithSpaces(4)
        paddedCell()
    }
}

jacocoTestReport {
    reports { xml.enabled true }
}

description 'web3j command line tools'
mainClassName = 'org.web3j.console.Runner'
applicationName = 'web3j'
